// time_glob.cpp - Globalno rukovanje vremenom
#include <Arduino.h>
#include <avr/pgmspace.h>
#include <RTClib.h>
#include "time_glob.h"
#include "eeprom_konstante.h"
#include "i2c_bus.h"
#include "podesavanja_piny.h"
#include "wear_leveling.h"
#include "pc_serial.h"
#include "kazaljke_sata.h"
#include "okretna_ploca.h"
#include "esp_serial.h"
#include "lcd_display.h"
#include "power_recovery.h"

// DS3231 RTC modul
static RTC_DS3231 rtc;

// Trenutno vrijeme (cache)
static DateTime trenutnoVrijeme;

// Izvor vremena toranjskog sata:
// - trenutniIzvor je ono sto sada stvarno prikazujemo i koristimo kao aktivni izvor rada
// - zadnjiPotvrdeniIzvor pamti odakle je dosla zadnja svjeza sinkronizacija
enum IzvorVremena {
  IZ_RTC,
  IZ_MAN,
  IZ_NTP
};

static IzvorVremena trenutniIzvor = IZ_RTC;
static IzvorVremena zadnjiPotvrdeniIzvor = IZ_RTC;

// RTC + NTP jezgra toranjskog sata namjerno je konzervativna.
// Pravila koja ne treba mijenjati bez stvarne potrebe:
// - nakon boota sat uvijek krece iz RTC-a, bez cekanja mreze
// - NTP samo potvrduje i korigira RTC, ne smije blokirati start sata
// - prikaz "NTP" smije se pojaviti tek nakon stvarno uspjesne nove sinkronizacije
// - ako vise od 24 sata nema nove sinkronizacije, prikaz i rad se vracaju na RTC
// - nakon povratka napajanja ESP/WiFi dobiva pocetnu odgodu prije prvog NTP pokusaja
// Svaka promjena ovdje mora cuvati taj tok rada.

// Zadnja sinkronizacija
static DateTime zadnjaSinkronizacija((uint32_t)0);
static unsigned long zadnjaSinkronizacijaMs = 0;
static const unsigned long TIMEOUT_SINKRONIZACIJE_MS = 86400000UL; // 24 sata

// Oznake
static char oznakaizvora[4] = "RTC";

// RTC pouzdanost
static bool rtcBaterijaOk = false;
static bool rtcSqwAktivan = false;
static bool rtcSqwGreskaPrijavljena = false;
static unsigned long rtcSqwZadnjiTickMs = 0;
static bool rtcDostupanNakonInicijalizacije = false;
static unsigned long rtcZadnjiPokusajReinicijalizacijeMs = 0;
static bool rtcIzlazniFailSafeAktivan = false;
static bool dstAktivan = false;
static bool dstStatusUcitan = false;
static bool dstStatusProcitanIzTrajnePohrane = false;
static bool vrijemePotvrdjenoZaAutomatiku = false;
static bool rtcDegradiraniNacinAktivan = false;
static bool ntpSinkronizacijaZakazana = false;
static DateTime zakazanoNtpVrijeme((uint32_t)0);
static uint16_t zakazanoNtpMilisekunde = 0;
static uint32_t zakazaniNtpRtcTick = 0;
static unsigned long zakazaniNtpMs = 0;
static bool zakazanoNtpImaEksplicitanDST = false;
static bool zakazanoNtpDstAktivan = false;
static bool sumnjivaSinkronizacijaNaCekanju = false;
static uint8_t sumnjiviIzvorSinkronizacije = IZ_RTC;
static DateTime sumnjivoVrijemeSinkronizacije((uint32_t)0);
static unsigned long sumnjivaSinkronizacijaMs = 0;
static uint32_t zadnjiRtcTickSaSvjezimVremenom = 0;
static uint32_t rtcZadnjiProcitaniUnix = 0;
static uint32_t rtcZadnjiProcitaniTick = 0;

volatile uint32_t rtcSekundniBrojac = 0;

static const uint16_t MIN_VALJANA_GODINA = 2024;
static const uint16_t MAX_VALJANA_GODINA = 2099;
static const uint32_t MAX_SKOK_BEZ_DODATNE_POTVRDE_S = 300UL;
static const uint32_t MAX_RAZLIKA_IZMEDU_DVIJE_POTVRDE_S = 3UL;
static const unsigned long ROK_SUMNJIVE_SINKRONIZACIJE_MS = 15UL * 60UL * 1000UL;
static const uint8_t RTC_DEGRADED_PRAG_NEUSPJEHA = 3;
static const uint8_t RTC_OPORAVAK_PRAG_USPJEHA = 3;
static const unsigned long RTC_REINICIJALIZACIJA_INTERVAL_MS = 5000UL;
static uint8_t rtcNeuspjesiZaredom = 0;
static uint8_t rtcUspjesiZaredom = 0;

void rtcSqwPrekid() {
  rtcSekundniBrojac++;
}

// -------------------- POMOCNE FUNKCIJE --------------------

static void azurirajOznakuIzvora() {
  switch (trenutniIzvor) {
    case IZ_RTC:
      strncpy(oznakaizvora, "RTC", sizeof(oznakaizvora) - 1);
      break;
    case IZ_MAN:
      strncpy(oznakaizvora, "MAN", sizeof(oznakaizvora) - 1);
      break;
    case IZ_NTP:
      strncpy(oznakaizvora, "NTP", sizeof(oznakaizvora) - 1);
      break;
    default:
      strncpy(oznakaizvora, "???", sizeof(oznakaizvora) - 1);
      break;
  }
  oznakaizvora[sizeof(oznakaizvora) - 1] = '\0';
}

static bool primijeniVrijemeIzNTP(DateTime ntpVrijeme,
                                  bool imaEksplicitanDST,
                                  bool dstAktivanIzvori);
static bool osvjeziTrenutnoVrijemeIzRtc();
static bool osigurajRtcDostupan(bool prisilniPokusaj);
static void oznaciPovratakNaRTC();
static void evidentirajRtcUspjeh();
static void evidentirajRtcNeuspjeh(const __FlashStringHelper* razlog);
static void postaviRtcIzlazniFailSafe(bool aktivan, const __FlashStringHelper* razlog);
static void resetirajRtcMonitorNapredovanja();

static bool jeVrijemeURasponuPouzdanosti(const DateTime& vrijeme) {
  return vrijeme.unixtime() != 0 &&
         vrijeme.year() >= MIN_VALJANA_GODINA &&
         vrijeme.year() <= MAX_VALJANA_GODINA;
}

static void ocistiSumnjivuSinkronizaciju() {
  sumnjivaSinkronizacijaNaCekanju = false;
  sumnjiviIzvorSinkronizacije = IZ_RTC;
  sumnjivoVrijemeSinkronizacije = DateTime((uint32_t)0);
  sumnjivaSinkronizacijaMs = 0;
}

static uint32_t apsolutnaRazlikaSekundi(const DateTime& a, const DateTime& b) {
  const uint32_t unixA = a.unixtime();
  const uint32_t unixB = b.unixtime();
  return (unixA >= unixB) ? (unixA - unixB) : (unixB - unixA);
}

static uint32_t izracunajPomakSekundiZaZakazaniNtp(unsigned long protekloMs,
                                                   uint16_t milisekundeNtp,
                                                   uint32_t minimalniPomakSekundi) {
  const unsigned long ukupnoMs = protekloMs + static_cast<unsigned long>(milisekundeNtp);
  uint32_t pomakSekundi = ukupnoMs / 1000UL;
  if (pomakSekundi < minimalniPomakSekundi) {
    pomakSekundi = minimalniPomakSekundi;
  }
  return pomakSekundi;
}

static bool pokusajUpisatiVrijemeURtc(const DateTime& novoVrijeme,
                                      const __FlashStringHelper* porukaNeuspjeha) {
  // RTC se inicijalizira samo jednom pri bootu toranjskog sata.
  // Kod kasnijeg upisa ne zovemo ponovno rtc.begin(), nego upis
  // odmah provjeravamo citanjem natrag iz RTC-a.
  if (!osigurajRtcDostupan(false)) {
    evidentirajRtcNeuspjeh(porukaNeuspjeha);
    signalizirajError_RTC();
    return false;
  }

  rtc.adjust(novoVrijeme);

  const DateTime potvrdaRtcVrijemena = rtc.now();
  if (!jeVrijemeURasponuPouzdanosti(potvrdaRtcVrijemena) ||
      apsolutnaRazlikaSekundi(potvrdaRtcVrijemena, novoVrijeme) > 1UL) {
    evidentirajRtcNeuspjeh(porukaNeuspjeha);
    signalizirajError_RTC();
    return false;
  }

  rtcBaterijaOk = true;
  rtcDegradiraniNacinAktivan = false;
  rtcNeuspjesiZaredom = 0;
  rtcUspjesiZaredom = RTC_OPORAVAK_PRAG_USPJEHA;
  resetirajRtcMonitorNapredovanja();
  return true;
}

static bool osigurajRtcDostupan(bool prisilniPokusaj) {
  if (rtcDostupanNakonInicijalizacije) {
    return true;
  }

  const unsigned long sadaMs = millis();
  if (!prisilniPokusaj &&
      rtcZadnjiPokusajReinicijalizacijeMs != 0 &&
      (sadaMs - rtcZadnjiPokusajReinicijalizacijeMs) < RTC_REINICIJALIZACIJA_INTERVAL_MS) {
    return false;
  }

  const bool bioRanijeNedostupan = (rtcZadnjiPokusajReinicijalizacijeMs != 0);
  rtcZadnjiPokusajReinicijalizacijeMs = sadaMs;
  pripremiI2CSabirnicuSigurno();
  if (!rtc.begin()) {
    rtcDostupanNakonInicijalizacije = false;
    postaviRtcIzlazniFailSafe(
        true,
        F("RTC fail-safe: gubitak RTC/I2C veze, odmah blokiram releje toranjskog sata"));
    return false;
  }

  rtcDostupanNakonInicijalizacije = true;
  rtc.writeSqwPinMode(DS3231_SquareWave1Hz);
  postaviRtcIzlazniFailSafe(
      false,
      F("RTC fail-safe: RTC/I2C veza oporavljena, releji toranjskog sata ponovno su dozvoljeni"));
  if (bioRanijeNedostupan) {
    posaljiPCLog(F("RTC: DS3231 ponovno dostupan, nastavljam rad toranjskog sata"));
  }
  return true;
}

static void kopirajFlashTekst(const __FlashStringHelper* tekst, char* odrediste, size_t velicina) {
  if (odrediste == nullptr || velicina == 0) {
    return;
  }

  if (tekst == nullptr) {
    odrediste[0] = '\0';
    return;
  }

  strncpy_P(odrediste, reinterpret_cast<const char*>(tekst), velicina - 1);
  odrediste[velicina - 1] = '\0';
}

static void formatirajDatumIVrijeme(const DateTime& vrijeme, char* odrediste, size_t velicina) {
  snprintf_P(odrediste, velicina,
             PSTR("%u-%02u-%02u %02u:%02u:%02u"),
             vrijeme.year(),
             vrijeme.month(),
             vrijeme.day(),
             vrijeme.hour(),
             vrijeme.minute(),
             vrijeme.second());
}

static RezultatProvjereSinkronizacije potvrdiIliOdbijSumnjivuSinkronizaciju(
    const DateTime& novoVrijeme,
    uint8_t izvor,
    const __FlashStringHelper* nazivIzvora) {
  if (!jeVrijemeURasponuPouzdanosti(novoVrijeme)) {
    char izvorTekst[8];
    char log[96];
    kopirajFlashTekst(nazivIzvora, izvorTekst, sizeof(izvorTekst));
    snprintf_P(log, sizeof(log), PSTR("%s: nevaljano vrijeme izvan dopustenog raspona, odbacujem"), izvorTekst);
    posaljiPCLog(log);
    ocistiSumnjivuSinkronizaciju();
    return SINKRONIZACIJA_ODBIJENA;
  }

  if (!vrijemePotvrdjenoZaAutomatiku || !jeVrijemeURasponuPouzdanosti(trenutnoVrijeme)) {
    ocistiSumnjivuSinkronizaciju();
    return SINKRONIZACIJA_PRIHVACENA;
  }

  const uint32_t razlikaSekundi = apsolutnaRazlikaSekundi(trenutnoVrijeme, novoVrijeme);
  if (razlikaSekundi <= MAX_SKOK_BEZ_DODATNE_POTVRDE_S) {
    ocistiSumnjivuSinkronizaciju();
    return SINKRONIZACIJA_PRIHVACENA;
  }

  const unsigned long sadaMs = millis();
  if (sumnjivaSinkronizacijaNaCekanju &&
      sumnjiviIzvorSinkronizacije == izvor &&
      jeVrijemeURasponuPouzdanosti(sumnjivoVrijemeSinkronizacije) &&
      (sadaMs - sumnjivaSinkronizacijaMs) <= ROK_SUMNJIVE_SINKRONIZACIJE_MS &&
      apsolutnaRazlikaSekundi(sumnjivoVrijemeSinkronizacije, novoVrijeme) <=
          MAX_RAZLIKA_IZMEDU_DVIJE_POTVRDE_S) {
    char izvorTekst[8];
    char log[80];
    kopirajFlashTekst(nazivIzvora, izvorTekst, sizeof(izvorTekst));
    snprintf_P(log, sizeof(log), PSTR("%s: potvrden sumnjiv skok vremena, prihvacam korekciju"), izvorTekst);
    posaljiPCLog(log);
    ocistiSumnjivuSinkronizaciju();
    return SINKRONIZACIJA_PRIHVACENA;
  }

  sumnjivaSinkronizacijaNaCekanju = true;
  sumnjiviIzvorSinkronizacije = izvor;
  sumnjivoVrijemeSinkronizacije = novoVrijeme;
  sumnjivaSinkronizacijaMs = sadaMs;

  char izvorTekst[8];
  char log[96];
  kopirajFlashTekst(nazivIzvora, izvorTekst, sizeof(izvorTekst));
  snprintf_P(log, sizeof(log),
             PSTR("%s: sumnjiv skok vremena od %lu s, cekam ponovljenu potvrdu prije upisa u RTC"),
             izvorTekst,
             razlikaSekundi);
  posaljiPCLog(log);

  if (izvor == IZ_NTP) {
    zatraziPrioritetnuNTPSinkronizaciju();
  }
  return SINKRONIZACIJA_CEKA_DODATNU_POTVRDU;
}

static void otkaziZakazanuNtpSinkronizaciju() {
  ntpSinkronizacijaZakazana = false;
  zakazanoNtpVrijeme = DateTime((uint32_t)0);
  zakazanoNtpMilisekunde = 0;
  zakazaniNtpRtcTick = 0;
  zakazaniNtpMs = 0;
  zakazanoNtpImaEksplicitanDST = false;
  zakazanoNtpDstAktivan = false;
}

static void spremiZadnjuSinkronizaciju() {
  if (jeEepromDegradiraniNacinAktivan()) {
    return;
  }

  EepromLayout::ZadnjaSinkronizacija zs{};
  zs.izvor = static_cast<uint8_t>(zadnjiPotvrdeniIzvor);
  zs.timestamp = zadnjaSinkronizacija.unixtime();
  zs.checksum = static_cast<uint8_t>(
      0xA5U ^
      zs.izvor ^
      static_cast<uint8_t>(zs.timestamp & 0xFFU) ^
      static_cast<uint8_t>((zs.timestamp >> 8) & 0xFFU) ^
      static_cast<uint8_t>((zs.timestamp >> 16) & 0xFFU) ^
      static_cast<uint8_t>((zs.timestamp >> 24) & 0xFFU));
  WearLeveling::spremi(EepromLayout::BAZA_ZADNJA_SINKRONIZACIJA,
                       EepromLayout::SLOTOVI_ZADNJA_SINKRONIZACIJA,
                       zs);
  zadnjaSinkronizacijaMs = millis();
}

static unsigned long izracunajZadnjuSinkronizacijuMsPriBootu(
    const DateTime& trenutnoRtcVrijeme,
    const DateTime& spremljenaSinkronizacija) {
  if (!jeVrijemeURasponuPouzdanosti(spremljenaSinkronizacija)) {
    return 0;
  }

  const unsigned long sadaMs = millis();
  const uint32_t timeoutSekunde = TIMEOUT_SINKRONIZACIJE_MS / 1000UL;

  if (!jeVrijemeURasponuPouzdanosti(trenutnoRtcVrijeme) ||
      trenutnoRtcVrijeme.unixtime() < spremljenaSinkronizacija.unixtime()) {
    // Ako RTC nakon restarta toranjskog sata nije pouzdan, sinkronizaciju tretiramo
    // kao zastarjelu umjesto da joj damo laznih 24 sata svjezine.
    return sadaMs - (TIMEOUT_SINKRONIZACIJE_MS + 1UL);
  }

  const uint32_t starostSekunde =
      trenutnoRtcVrijeme.unixtime() - spremljenaSinkronizacija.unixtime();
  if (starostSekunde > timeoutSekunde) {
    return sadaMs - (TIMEOUT_SINKRONIZACIJE_MS + 1UL);
  }

  return sadaMs - (starostSekunde * 1000UL);
}

static uint16_t izracunajDSTChecksum(const EepromLayout::DSTStatus& stanje) {
  uint16_t checksum = 0;
  checksum += stanje.potpis;
  checksum += stanje.dstAktivan;
  checksum += stanje.reserved;
  return checksum;
}

static bool jeDSTStatusValjan(const EepromLayout::DSTStatus& stanje) {
  return stanje.potpis == EepromLayout::DST_STATUS_POTPIS &&
         stanje.checksum == izracunajDSTChecksum(stanje) &&
         stanje.dstAktivan <= 1;
}

static uint8_t izracunajChecksumZadnjeSinkronizacije(
    const EepromLayout::ZadnjaSinkronizacija& stanje) {
  return static_cast<uint8_t>(
      0xA5U ^
      stanje.izvor ^
      static_cast<uint8_t>(stanje.timestamp & 0xFFU) ^
      static_cast<uint8_t>((stanje.timestamp >> 8) & 0xFFU) ^
      static_cast<uint8_t>((stanje.timestamp >> 16) & 0xFFU) ^
      static_cast<uint8_t>((stanje.timestamp >> 24) & 0xFFU));
}

static bool jeZadnjaSinkronizacijaValjana(const EepromLayout::ZadnjaSinkronizacija& stanje) {
  return (stanje.izvor == IZ_RTC ||
          stanje.izvor == IZ_MAN ||
          stanje.izvor == IZ_NTP) &&
         stanje.checksum == izracunajChecksumZadnjeSinkronizacije(stanje);
}

static uint8_t zadnjaNedjeljaUMjesecu(int godina, uint8_t mjesec) {
  DateTime zadnjiDan(godina, mjesec + 1, 1, 0, 0, 0);
  zadnjiDan = DateTime(zadnjiDan.unixtime() - 86400UL);
  while (zadnjiDan.dayOfTheWeek() != 0) {
    zadnjiDan = DateTime(zadnjiDan.unixtime() - 86400UL);
  }
  return zadnjiDan.day();
}

static bool izracunajDSTIzKalendara(const DateTime& vrijeme) {
  const uint8_t mjesec = vrijeme.month();
  if (mjesec < 3 || mjesec > 10) return false;
  if (mjesec > 3 && mjesec < 10) return true;

  const uint8_t zadnjaNedjelja = zadnjaNedjeljaUMjesecu(vrijeme.year(), mjesec);
  if (mjesec == 3) {
    if (vrijeme.day() < zadnjaNedjelja) return false;
    if (vrijeme.day() > zadnjaNedjelja) return true;
    return vrijeme.hour() >= 3;
  }

  if (vrijeme.day() < zadnjaNedjelja) return true;
  if (vrijeme.day() > zadnjaNedjelja) return false;
  return vrijeme.hour() < 3;
}

static bool odrediDSTStatusSinkronizacije(const DateTime& vrijeme,
                                          bool imaEksplicitanDST,
                                          bool dstAktivanIzvori) {
  if (imaEksplicitanDST) {
    return dstAktivanIzvori;
  }

  return izracunajDSTIzKalendara(vrijeme);
}

static void spremiDSTStatus() {
  if (jeEepromDegradiraniNacinAktivan()) {
    return;
  }

  EepromLayout::DSTStatus stanje{};
  stanje.potpis = EepromLayout::DST_STATUS_POTPIS;
  stanje.dstAktivan = dstAktivan ? 1 : 0;
  stanje.reserved = 0;
  stanje.checksum = izracunajDSTChecksum(stanje);
  WearLeveling::spremi(EepromLayout::BAZA_DST_STATUS,
                       EepromLayout::SLOTOVI_DST_STATUS,
                       stanje);
  dstStatusUcitan = true;
}

static void ucitajDSTStatus() {
  EepromLayout::DSTStatus stanje{};
  dstStatusProcitanIzTrajnePohrane = false;
  if (WearLeveling::ucitaj(EepromLayout::BAZA_DST_STATUS,
                           EepromLayout::SLOTOVI_DST_STATUS,
                           stanje) &&
      jeDSTStatusValjan(stanje)) {
    dstAktivan = stanje.dstAktivan != 0;
    dstStatusUcitan = true;
    dstStatusProcitanIzTrajnePohrane = true;
    return;
  }

  dstAktivan = izracunajDSTIzKalendara(trenutnoVrijeme);
  dstStatusUcitan = true;
  spremiDSTStatus();
}

static void uskladiPropusteniDSTPrijelazNakonBoota() {
  if (!rtcBaterijaOk ||
      !dstStatusProcitanIzTrajnePohrane ||
      !jeVrijemeURasponuPouzdanosti(trenutnoVrijeme)) {
    return;
  }

  const bool kalendarskiDST = izracunajDSTIzKalendara(trenutnoVrijeme);
  if (kalendarskiDST == dstAktivan) {
    return;
  }

  const uint32_t trenutnoUnix = trenutnoVrijeme.unixtime();
  if (!kalendarskiDST && trenutnoUnix < 3600UL) {
    return;
  }

  const DateTime korigiranoVrijeme(
      kalendarskiDST ? (trenutnoUnix + 3600UL) : (trenutnoUnix - 3600UL));
  if (!pokusajUpisatiVrijemeURtc(
          korigiranoVrijeme,
          F("RTC: neuspjela offline DST korekcija, cekam NTP ili rucni unos"))) {
    return;
  }

  trenutnoVrijeme = korigiranoVrijeme;
  dstAktivan = kalendarskiDST;
  spremiDSTStatus();

  posaljiPCLog(kalendarskiDST
                   ? F("DST: offline korekcija propustenog prijelaza na CEST (+60 min)")
                   : F("DST: offline korekcija propustenog prijelaza na CET (-60 min)"));
}

static bool trebaProljetniPomak(const DateTime& vrijeme) {
  return !dstAktivan &&
         vrijeme.month() == 3 &&
         vrijeme.day() == zadnjaNedjeljaUMjesecu(vrijeme.year(), 3) &&
         vrijeme.hour() == 2;
}

static bool trebaJesenskiPomak(const DateTime& vrijeme) {
  return dstAktivan &&
         vrijeme.month() == 10 &&
         vrijeme.day() == zadnjaNedjeljaUMjesecu(vrijeme.year(), 10) &&
         vrijeme.hour() == 3;
}

static void primijeniDSTPomak(int pomakSekundi, bool noviDSTAktivan, const __FlashStringHelper* opis) {
  DateTime novoVrijeme(trenutnoVrijeme.unixtime() + pomakSekundi);
  if (!pokusajUpisatiVrijemeURtc(
          novoVrijeme,
          F("RTC: neuspjela DST korekcija, ostajem u degradiranom nacinu rada"))) {
    return;
  }

  trenutnoVrijeme = novoVrijeme;
  dstAktivan = noviDSTAktivan;
  spremiDSTStatus();

  posaljiPCLog(opis);
  obavijestiKazaljkeDSTPromjena(pomakSekundi / 60);
  zatraziPoravnanjeTaktaKazaljki();
  zatraziPoravnanjeTaktaPloce();
}

static void obradiAutomatskiDST() {
  if (!dstStatusUcitan) {
    ucitajDSTStatus();
  }

  if (trebaProljetniPomak(trenutnoVrijeme)) {
    primijeniDSTPomak(3600, true, F("DST: automatski prijelaz na CEST (+60 min)"));
    return;
  }

  if (trebaJesenskiPomak(trenutnoVrijeme)) {
    primijeniDSTPomak(-3600, false, F("DST: automatski prijelaz na CET (-60 min)"));
  }
}

static void ucitajZadnjuSinkronizaciju() {
  EepromLayout::ZadnjaSinkronizacija zs{};
  const bool imaValjaniZapis =
      WearLeveling::ucitaj(EepromLayout::BAZA_ZADNJA_SINKRONIZACIJA,
                           EepromLayout::SLOTOVI_ZADNJA_SINKRONIZACIJA,
                           zs) &&
      jeZadnjaSinkronizacijaValjana(zs);

  if (imaValjaniZapis) {
    zadnjaSinkronizacija = DateTime(zs.timestamp);
    zadnjaSinkronizacijaMs =
        izracunajZadnjuSinkronizacijuMsPriBootu(trenutnoVrijeme, zadnjaSinkronizacija);

    if (zs.izvor == IZ_MAN) {
      zadnjiPotvrdeniIzvor = IZ_MAN;
    } else if (zs.izvor == IZ_NTP) {
      zadnjiPotvrdeniIzvor = IZ_NTP;
    } else {
      zadnjiPotvrdeniIzvor = IZ_RTC;
    }
  } else {
    zadnjaSinkronizacija = DateTime((uint32_t)0);
    zadnjaSinkronizacijaMs = 0;
    zadnjiPotvrdeniIzvor = IZ_RTC;
  }
  // Nakon boota sat realno radi iz RTC-a dok ne stigne nova potvrda izvora.
  trenutniIzvor = IZ_RTC;
  azurirajOznakuIzvora();
}

static bool osvjeziTrenutnoVrijemeIzRtc() {
  if (!osigurajRtcDostupan(false)) {
    evidentirajRtcNeuspjeh(
        F("RTC: DS3231 nije dostupan tijekom osvjezavanja, ostajem u degradiranom nacinu rada"));
    signalizirajError_RTC();
    return false;
  }

  const DateTime rtcVrijeme = rtc.now();
  if (!jeVrijemeURasponuPouzdanosti(rtcVrijeme)) {
    evidentirajRtcNeuspjeh(
        F("RTC: uzastopna nevaljana ocitanja, ulazim u degradirani nacin rada"));
    signalizirajError_RTC();
    return false;
  }

  const uint32_t rtcVrijemeUnix = rtcVrijeme.unixtime();
  const uint32_t lokalniRtcTick = dohvatiRtcSekundniBrojac();
  if (rtcSqwAktivan &&
      rtcZadnjiProcitaniTick != 0 &&
      lokalniRtcTick != rtcZadnjiProcitaniTick) {
    if (rtcVrijemeUnix <= rtcZadnjiProcitaniUnix) {
      postaviRtcIzlazniFailSafe(
          true,
          F("RTC fail-safe: SQW radi, ali RTC vrijeme ne napreduje; blokiram releje toranjskog sata"));
      evidentirajRtcNeuspjeh(
          F("RTC: SQW impulsi postoje, ali se procitano RTC vrijeme ne mijenja"));
      signalizirajError_RTC();
      return false;
    }
  }

  trenutnoVrijeme = rtcVrijeme;
  rtcZadnjiProcitaniUnix = rtcVrijemeUnix;
  rtcZadnjiProcitaniTick = lokalniRtcTick;
  evidentirajRtcUspjeh();
  return true;
}

static void postaviVrijemePotvrdjenoZaAutomatiku(bool potvrdeno, const __FlashStringHelper* razlog) {
  if (vrijemePotvrdjenoZaAutomatiku == potvrdeno && razlog == nullptr) {
    return;
  }

  vrijemePotvrdjenoZaAutomatiku = potvrdeno;
  if (razlog != nullptr) {
    posaljiPCLog(razlog);
  }
}

static void resetirajRtcMonitorNapredovanja() {
  rtcZadnjiProcitaniUnix = 0;
  rtcZadnjiProcitaniTick = 0;
}

static void postaviRtcIzlazniFailSafe(bool aktivan, const __FlashStringHelper* razlog) {
  if (rtcIzlazniFailSafeAktivan == aktivan) {
    return;
  }

  rtcIzlazniFailSafeAktivan = aktivan;
  if (razlog != nullptr) {
    posaljiPCLog(razlog);
  }
}

static void evidentirajRtcUspjeh() {
  rtcNeuspjesiZaredom = 0;
  postaviRtcIzlazniFailSafe(
      false,
      F("RTC fail-safe: valjano RTC ocitanje vraceno, releji toranjskog sata ponovno su dozvoljeni"));
  if (rtcUspjesiZaredom < 255U) {
    ++rtcUspjesiZaredom;
  }

  if (!rtcDegradiraniNacinAktivan || rtcUspjesiZaredom < RTC_OPORAVAK_PRAG_USPJEHA) {
    return;
  }

  rtcDegradiraniNacinAktivan = false;
  if (rtcBaterijaOk) {
    postaviVrijemePotvrdjenoZaAutomatiku(
        true,
        F("RTC: izlazim iz degradiranog nacina rada nakon uzastopnih valjanih ocitanja"));
  } else {
    posaljiPCLog(F("RTC: ocitanje se oporavilo, ali vrijeme jos nije potvrdeno zbog RTC baterije"));
  }
}

static void evidentirajRtcNeuspjeh(const __FlashStringHelper* razlog) {
  rtcUspjesiZaredom = 0;
  postaviRtcIzlazniFailSafe(
      true,
      F("RTC fail-safe: nevaljano RTC ocitanje, odmah blokiram releje toranjskog sata"));
  if (rtcNeuspjesiZaredom < 255U) {
    ++rtcNeuspjesiZaredom;
  }

  if (rtcDegradiraniNacinAktivan || rtcNeuspjesiZaredom < RTC_DEGRADED_PRAG_NEUSPJEHA) {
    return;
  }

  rtcDegradiraniNacinAktivan = true;
  oznaciPovratakNaRTC();
  postaviVrijemePotvrdjenoZaAutomatiku(
      false,
      F("RTC: degradirani nacin rada, automatika toranjskog sata je privremeno blokirana"));
  signalizirajError_RTC();
  posaljiPCLog(razlog);
}

DateTime dohvatiDatumUskrsaZaGodinu(int godina) {
  const int a = godina % 19;
  const int b = godina / 100;
  const int c = godina % 100;
  const int d = b / 4;
  const int e = b % 4;
  const int f = (b + 8) / 25;
  const int g = (b - f + 1) / 3;
  const int h = (19 * a + b - d - g + 15) % 30;
  const int i = c / 4;
  const int k = c % 4;
  const int l = (32 + (2 * e) + (2 * i) - h - k) % 7;
  const int m = (a + (11 * h) + (22 * l)) / 451;
  const int mjesec = (h + l - (7 * m) + 114) / 31;
  const int dan = ((h + l - (7 * m) + 114) % 31) + 1;
  return DateTime(godina, mjesec, dan, 0, 0, 0);
}

// -------------------- JAVNE FUNKCIJE --------------------

void inicijalizirajRTC() {
  pripremiI2CSabirnicuSigurno();
  rtcZadnjiPokusajReinicijalizacijeMs = 0;
  if (!osigurajRtcDostupan(true)) {
    rtcDostupanNakonInicijalizacije = false;
    rtcDegradiraniNacinAktivan = true;
    rtcNeuspjesiZaredom = RTC_DEGRADED_PRAG_NEUSPJEHA;
    rtcUspjesiZaredom = 0;
    posaljiPCLog(F("RTC: DS3231 nije dostupan"));
    rtcBaterijaOk = false;
    postaviVrijemePotvrdjenoZaAutomatiku(
        false,
        F("Vrijeme: nije potvrdeno, automatika toranjskog sata ostaje blokirana"));
    signalizirajError_RTC();
  } else {
    rtcDostupanNakonInicijalizacije = true;
    rtcDegradiraniNacinAktivan = false;
    rtcNeuspjesiZaredom = 0;
    rtcUspjesiZaredom = 0;
    // Provjera valjanosti RTC baterije
    if (rtc.lostPower()) {
      posaljiPCLog(F("RTC: baterija je prazna, vrijeme nije pouzdano"));
      rtcBaterijaOk = false;
      postaviVrijemePotvrdjenoZaAutomatiku(
          false,
          F("Vrijeme: cekam NTP ili rucnu potvrdu prije aktivacije automatike"));
      signalizirajUpozorenjeRtcBaterije();
      posaljiPCLog(F("RTC: ne prepisujem vrijeme dok ga ne potvrdi NTP ili rucni unos"));
    } else {
      rtcBaterijaOk = true;
      posaljiPCLog(F("RTC: baterija OK, vrijeme je pouzdano"));
    }
    if (osvjeziTrenutnoVrijemeIzRtc()) {
      if (rtcBaterijaOk) {
        postaviVrijemePotvrdjenoZaAutomatiku(true, nullptr);
      }
    } else if (rtcBaterijaOk) {
      postaviVrijemePotvrdjenoZaAutomatiku(
          false,
          F("RTC: pocetno ocitanje nije uspjelo, cekam novu potvrdu vremena"));
    }
  }
  pinMode(PIN_RTC_SQW, INPUT_PULLUP);
  attachInterrupt(digitalPinToInterrupt(PIN_RTC_SQW), rtcSqwPrekid, FALLING);
  rtcSqwAktivan = false;
  rtcSqwGreskaPrijavljena = false;
  rtcSqwZadnjiTickMs = millis();
  posaljiPCLog(F("RTC: pocinjem ucitavanje DST statusa"));
  ucitajDSTStatus();
  uskladiPropusteniDSTPrijelazNakonBoota();
  posaljiPCLog(F("RTC: DST status ucitan"));
  posaljiPCLog(F("RTC: pocinjem ucitavanje zadnje sinkronizacije"));
  ucitajZadnjuSinkronizaciju();
  posaljiPCLog(F("RTC: zadnja sinkronizacija ucitana"));
  if (!vrijemePotvrdjenoZaAutomatiku) {
    trenutniIzvor = IZ_RTC;
    azurirajOznakuIzvora();
  }
}

DateTime dohvatiTrenutnoVrijeme() {
  static uint32_t zadnjiObradeniRtcTick = 0xFFFFFFFFUL;
  static unsigned long zadnjiFallbackPollMs = 0;
  uint32_t lokalniRtcTick = 0;
  const unsigned long sadaMs = millis();
  noInterrupts();
  lokalniRtcTick = rtcSekundniBrojac;
  interrupts();

  if (lokalniRtcTick != zadnjiObradeniRtcTick || zadnjiObradeniRtcTick == 0xFFFFFFFFUL) {
    zadnjiObradeniRtcTick = lokalniRtcTick;
    rtcSqwAktivan = true;
    rtcSqwZadnjiTickMs = sadaMs;
    if (rtcSqwGreskaPrijavljena) {
      posaljiPCLog(F("RTC SQW: impulsi na D2 ponovno prisutni"));
      rtcSqwGreskaPrijavljena = false;
    }
    bool ntpPrimijenjenNaOvomTicku = false;
    if (ntpSinkronizacijaZakazana) {
      if (zakazaniNtpRtcTick == 0) {
        zakazaniNtpRtcTick = lokalniRtcTick;
        posaljiPCLog(
            F("NTP: RTC SQW se vratio, sinkronizacija ceka prvi sljedeci valjani SQW tik"));
      }

      uint32_t protekliRtcTickovi = 1;
      unsigned long protekloOdZakazanogMs = 0;
      bool ntpSpremanZaPrimjenu = false;
      // NTP sinkronizacija mora cekati stvarno sljedeci RTC SQW tik.
      // Ako bismo prihvatili i isti rtcTick na kojem je sinkronizacija zakazana,
      // toranjski sat bi u rubnom slucaju mogao zavrsiti jednu sekundu unaprijed.
      if (zakazaniNtpRtcTick != 0 && lokalniRtcTick > zakazaniNtpRtcTick) {
        protekliRtcTickovi = lokalniRtcTick - zakazaniNtpRtcTick;
        protekloOdZakazanogMs = sadaMs - zakazaniNtpMs;
        if (protekliRtcTickovi == 0) {
          protekliRtcTickovi = 1;
        }
        ntpSpremanZaPrimjenu = true;
      }

      if (ntpSpremanZaPrimjenu) {
        const uint32_t minimalniPomakSekundi =
            (protekliRtcTickovi > 0U) ? (protekliRtcTickovi - 1U) : 0U;
        const uint32_t precizniPomakSekundi =
            izracunajPomakSekundiZaZakazaniNtp(
                protekloOdZakazanogMs,
                zakazanoNtpMilisekunde,
                minimalniPomakSekundi);
        const DateTime vrijemeZaPrimjenu(
            zakazanoNtpVrijeme.unixtime() + precizniPomakSekundi);
        if (primijeniVrijemeIzNTP(vrijemeZaPrimjenu,
                                  zakazanoNtpImaEksplicitanDST,
                                  zakazanoNtpDstAktivan)) {
          ntpPrimijenjenNaOvomTicku = true;
          posaljiPCLog(F("NTP: sinkronizacija primijenjena na RTC SQW granici sekunde"));
        }
      }
    }
    if (ntpPrimijenjenNaOvomTicku) {
      zadnjiRtcTickSaSvjezimVremenom = lokalniRtcTick;
    } else if (osvjeziTrenutnoVrijemeIzRtc()) {
      zadnjiRtcTickSaSvjezimVremenom = lokalniRtcTick;
    }
  } else if ((sadaMs - rtcSqwZadnjiTickMs) > 2500UL) {
    rtcSqwAktivan = false;
    if (!rtcSqwGreskaPrijavljena) {
      posaljiPCLog(F("RTC SQW: nema 1 Hz impulsa na D2, mehanika toranjskog sata ostaje blokirana do povratka SQW"));
      rtcSqwGreskaPrijavljena = true;
    }
    if ((sadaMs - zadnjiFallbackPollMs) >= 1000UL || zadnjiFallbackPollMs == 0) {
      zadnjiFallbackPollMs = sadaMs;
      osvjeziTrenutnoVrijemeIzRtc();
    }
  }

  if (zadnjiPotvrdeniIzvor == IZ_NTP && jeSinkronizacijaZastarjela()) {
    oznaciPovratakNaRTC();
  }

  obradiAutomatskiDST();

  return trenutnoVrijeme;
}

uint32_t dohvatiRtcSekundniBrojac() {
  uint32_t lokalniRtcTick = 0;
  noInterrupts();
  lokalniRtcTick = rtcSekundniBrojac;
  interrupts();
  return lokalniRtcTick;
}

bool jeVrijemeSvjezeZaRtcTick(uint32_t rtcTick) {
  return rtcTick != 0 && zadnjiRtcTickSaSvjezimVremenom == rtcTick;
}

static bool primijeniVrijemeIzNTP(DateTime ntpVrijeme,
                                  bool imaEksplicitanDST,
                                  bool dstAktivanIzvori) {
  if (!jeVrijemeURasponuPouzdanosti(ntpVrijeme)) {
    posaljiPCLog(F("NTP: odbijena interna primjena nevaljanog vremena"));
    otkaziZakazanuNtpSinkronizaciju();
    return false;
  }

  otkaziZakazanuNtpSinkronizaciju();

  if (!pokusajUpisatiVrijemeURtc(
          ntpVrijeme,
          F("RTC: neuspjela NTP primjena na RTC, ostajem u degradiranom nacinu rada"))) {
    return false;
  }

  trenutnoVrijeme = ntpVrijeme;
  dstAktivan = odrediDSTStatusSinkronizacije(
      ntpVrijeme,
      imaEksplicitanDST,
      dstAktivanIzvori);
  spremiDSTStatus();
  zadnjaSinkronizacija = ntpVrijeme;
  trenutniIzvor = IZ_NTP;
  zadnjiPotvrdeniIzvor = IZ_NTP;
  ocistiSumnjivuSinkronizaciju();
  postaviVrijemePotvrdjenoZaAutomatiku(
      true,
      F("Vrijeme: potvrdeno iz NTP-a, automatika toranjskog sata je aktivna"));
  azurirajOznakuIzvora();
  spremiZadnjuSinkronizaciju();

  char vrijemeTekst[24];
  char log[64];
  formatirajDatumIVrijeme(ntpVrijeme, vrijemeTekst, sizeof(vrijemeTekst));
  snprintf_P(log, sizeof(log), PSTR("Vrijeme azurirano iz NTP: %s"), vrijemeTekst);
  posaljiPCLog(log);
  return true;
}

void azurirajVrijemeIzNTP(const DateTime& ntpVrijeme,
                          uint16_t ntpMilisekunde,
                          bool imaEksplicitanDST,
                          bool dstAktivanIzvori) {
  // Provjera validnosti NTP vremena
  if (!jeVrijemeURasponuPouzdanosti(ntpVrijeme)) {
    posaljiPCLog(F("NTP: neispravno vrijeme, odbacujem"));
    return;
  }

  if (potvrdiIliOdbijSumnjivuSinkronizaciju(ntpVrijeme, IZ_NTP, F("NTP")) !=
      SINKRONIZACIJA_PRIHVACENA) {
    return;
  }

  // Za precizniji trenutak sinkronizacije NTP se poravnava na iduci RTC SQW tik.
  if (rtcSqwAktivan) {
    zakazanoNtpVrijeme = ntpVrijeme;
    zakazanoNtpMilisekunde = ntpMilisekunde;
    ntpSinkronizacijaZakazana = true;
    zakazaniNtpRtcTick = dohvatiRtcSekundniBrojac();
    zakazaniNtpMs = millis();
    zakazanoNtpImaEksplicitanDST = imaEksplicitanDST;
    zakazanoNtpDstAktivan = dstAktivanIzvori;

    char vrijemeTekst[24];
    char log[96];
    formatirajDatumIVrijeme(ntpVrijeme, vrijemeTekst, sizeof(vrijemeTekst));
    snprintf_P(log, sizeof(log),
               PSTR("NTP: sinkronizacija zakazana na sljedeci RTC SQW tik od %s"),
               vrijemeTekst);
    posaljiPCLog(log);
    return;
  }

  // Ako RTC SQW trenutno nije dostupan, sinkronizaciju zadrzi na cekanju
  // i primijeni je tek nakon povratka valjanog SQW takta.
  zakazanoNtpVrijeme = ntpVrijeme;
  zakazanoNtpMilisekunde = ntpMilisekunde;
  ntpSinkronizacijaZakazana = true;
  zakazaniNtpRtcTick = 0;
  zakazaniNtpMs = millis();
  zakazanoNtpImaEksplicitanDST = imaEksplicitanDST;
  zakazanoNtpDstAktivan = dstAktivanIzvori;
  posaljiPCLog(F("NTP: RTC SQW nije aktivan, sinkronizacija ceka povratak SQW signala"));
  return;
}

void azurirajVrijemeRucno(const DateTime& rucnoVrijeme) {
  otkaziZakazanuNtpSinkronizaciju();

  if (!jeVrijemeURasponuPouzdanosti(rucnoVrijeme)) {
    posaljiPCLog(F("Rucno vrijeme: neispravna vrijednost, odbacujem"));
    return;
  }

  if (!pokusajUpisatiVrijemeURtc(
          rucnoVrijeme,
          F("RTC: neuspjelo rucno postavljanje vremena na RTC, ostajem u degradiranom nacinu rada"))) {
    return;
  }

  trenutnoVrijeme = rucnoVrijeme;
  dstAktivan = izracunajDSTIzKalendara(rucnoVrijeme);
  spremiDSTStatus();
  zadnjaSinkronizacija = rucnoVrijeme;
  trenutniIzvor = IZ_MAN;
  zadnjiPotvrdeniIzvor = IZ_MAN;
  ocistiSumnjivuSinkronizaciju();
  postaviVrijemePotvrdjenoZaAutomatiku(
      true,
      F("Vrijeme: rucno potvrdeno, automatika toranjskog sata je aktivna"));
  azurirajOznakuIzvora();
  spremiZadnjuSinkronizaciju();

  char vrijemeTekst[24];
  char log[72];
  formatirajDatumIVrijeme(rucnoVrijeme, vrijemeTekst, sizeof(vrijemeTekst));
  snprintf_P(log, sizeof(log), PSTR("Vrijeme rucno postavljeno: %s"), vrijemeTekst);
  posaljiPCLog(log);
  zatraziPoravnanjeTaktaKazaljki();
  zatraziPoravnanjeTaktaPloce();
  zatraziPrioritetnuNTPSinkronizaciju();
}

const char* dohvatiOznakuIzvoraVremena() {
  return oznakaizvora;
}

bool jeRtcSqwAktivan() {
  return rtcSqwAktivan;
}

bool jeRtcSqwGreskaAktivna() {
  return !rtcSqwAktivan &&
         rtcDostupanNakonInicijalizacije &&
         !rtcIzlazniFailSafeAktivan &&
         !rtcDegradiraniNacinAktivan;
}

bool jeRtcSqwPrvaPolovicaSekunde() {
  if (!rtcSqwAktivan) {
    return ((millis() / 500UL) % 2UL) == 0UL;
  }

  return (millis() - rtcSqwZadnjiTickMs) < 500UL;
}

bool dohvatiRtcTemperaturu(float& temperaturaC) {
  if (!osigurajRtcDostupan(false)) {
    return false;
  }

  temperaturaC = rtc.getTemperature();
  return true;
}

bool jeRtcDegradiraniNacinAktivan() {
  return rtcDegradiraniNacinAktivan;
}

bool jeRtcIzlazniFailSafeAktivan() {
  return rtcIzlazniFailSafeAktivan;
}

bool jeVrijemePotvrdjenoZaAutomatiku() {
  return vrijemePotvrdjenoZaAutomatiku;
}

bool jeUskrsnaTisinaAktivna(const DateTime& vrijeme) {
  if (vrijeme.unixtime() == 0) {
    return false;
  }

  const DateTime uskrs = dohvatiDatumUskrsaZaGodinu(vrijeme.year());
  const DateTime pocetakTisine(uskrs.unixtime() - (3UL * 86400UL) + (19UL * 3600UL));
  const DateTime krajTisine(uskrs.unixtime() - 86400UL + (22UL * 3600UL));
  const uint32_t sada = vrijeme.unixtime();

  return sada >= pocetakTisine.unixtime() && sada < krajTisine.unixtime();
}

int dohvatiUTCOffsetMinuteZaLokalnoVrijeme(const DateTime& vrijeme) {
  return izracunajDSTIzKalendara(vrijeme) ? 120 : 60;
}

static void oznaciPovratakNaRTC() {
  // Ako je sat radio iz potvrdenog NTP izvora, vrati prikaz i rad na RTC.
  if (trenutniIzvor != IZ_RTC) {
    trenutniIzvor = IZ_RTC;
    azurirajOznakuIzvora();
    posaljiPCLog(F("Povratak na RTC nakon vise od 24 sata bez nove NTP sinkronizacije"));
  }
}

void resetirajIzvorSinkronizacijeNaRTC() {
  zadnjaSinkronizacija = DateTime((uint32_t)0);
  zadnjaSinkronizacijaMs = 0;
  trenutniIzvor = IZ_RTC;
  zadnjiPotvrdeniIzvor = IZ_RTC;
  ocistiSumnjivuSinkronizaciju();
  azurirajOznakuIzvora();
  posaljiPCLog(F("Vrijeme: izvor sinkronizacije vracen na RTC"));
}

bool jeSinkronizacijaZastarjela() {
  if (!jeVrijemeURasponuPouzdanosti(zadnjaSinkronizacija)) {
    return true;
  }

  if (zadnjaSinkronizacijaMs == 0) {
    if (!jeVrijemeURasponuPouzdanosti(trenutnoVrijeme)) {
      return true;
    }

    uint32_t starostSekunde = 0;
    if (trenutnoVrijeme.unixtime() >= zadnjaSinkronizacija.unixtime()) {
      starostSekunde = trenutnoVrijeme.unixtime() - zadnjaSinkronizacija.unixtime();
    }

    return starostSekunde > (TIMEOUT_SINKRONIZACIJE_MS / 1000UL);
  }

  unsigned long sadaMs = millis();
  // Zastarjela ako je proslo vise od 24 sata
  return (sadaMs - zadnjaSinkronizacijaMs) > TIMEOUT_SINKRONIZACIJE_MS;
}

